# Day 22: Monkey map

**[Home](README.md)** - **[Description](#description)** - **Files:** [Solution to day 22](22.pl), [Input for day 22](data/22.txt), [Output for day 22](out/22.txt).

## Solution

***THIS IS MESSY***

**Note:** Code for [folding the cube](#folding_the_cube) {may be really in efficient} is posted at the end 

Initialise data structures - and find the size of each cube face...

```perl
my(@dir,@res) = ([1,0]); push @dir,[-$dir[-1][1],$dir[-1][0]] for 1..3;
my @in = map { chomp; $_ } <>;
my @path = split /([LR])/, pop @in;
pop @in;
my $width=0; length$_>$width && ($width=length$_) for @in;
my $height = ( my @map = ( [ (' ')x ($width+2) ],
  map( {[ @{ [' ',(split//), (' ') x $width ] }[0..($width+1)] ]} @in),
[ (' ')x ($width+2 ) ] ) ) - 2;
my $size = $height*3==$width*4 ? $width/3
         : $height*4==$width*3 ? $height/3
         : $height*5==$width*2 ? $width/5
         :                       $height/5
         ;
```

Compute the bounds of each row { used to compute jump map below }

```perl
my (@jump, @cube, @mx_x, @mx_y, @mn_x, @mn_y);
for my $r ( 1 .. $height ) {
  for my $c ( 1 .. $width ) {
    next if $map[$r][$c] eq  ' ';
    $mx_x[$r]=  $c; $mn_x[$r]||=$c;
    $mx_y[$c]=  $r; $mn_y[$c]||=$r;
  }
}
```
Fold the cube up!! OK this is a hack...

```perl
my @cube_join = (
  [ undef,                                  [ undef,   undef,   [2,0,0], [3,0,0] ], [ [2,1,2], [1,1,2], undef,   [3,0,3] ] ],
  [ undef,                                  [ [0,2,3], undef,   [2,0,1], undef   ], undef                                  ],
  [ [ undef,   undef,   [0,1,0], [1,1,0] ], [ [0,2,2], [3,0,2], undef,   undef   ], undef                                  ],
  [ [ [2,1,3], [0,2,1], [0,1,1], undef   ], undef,                                  undef                                  ],
);
```

Now we have to generate "teleporters" on the squares that you would step off the edge for. They contain
the location and direction on landing. For the toroidal map we use the min-max values - for the cubic
map we use the table above..

```perl
## Generate jump maps for the two "folds" of the map - when you jum off into space where do you go...
## @jump is for the toroidal map - and just rotates to the other side
## @cube is for the cube map - and requires a fold map is generated {hand crafted above}
for my $r ( 1 .. $height ) {
  my $fr = int(($r-1)/$size);
  for my $c ( 1 .. $width ) {
    next unless $map[$r][$c] eq '.';
    my $fc = int(($c-1)/$size);
    for my $d ( 0..3 ) {
      next if $map[ $r+$dir[$d][1] ][ $c+$dir[$d][0] ] ne ' ';
## Toroidal map
      my $dest = $d==0 ? [ $r,        $mn_x[$r], $d ]
               : $d==1 ? [ $mn_y[$c], $c,        $d ]
               : $d==2 ? [ $r,        $mx_x[$r], $d ]
               : $d==3 ? [ $mx_y[$c], $c,        $d ]
               : -1;
      $jump[$d]{$c}{$r} = $map[$dest->[0]][$dest->[1]] eq '#' ? undef : $dest;
## Cubic map
      my($dr,$dc,$dd) = @{ $cube_join[ $fr ][ $fc ][ $d ] };
      my $pos = $d == 0 ? $r - $fr * $size - 1
              : $d == 1 ? ($fc+1)*$size - $c
              : $d == 2 ? ($fr+1)*$size - $r
              :           $c - $fc * $size - 1
              ;
      my($nr,$nc) = $dd == 0 ? ( $dr * $size + 1 + $pos, $dc * $size + 1        )
                  : $dd == 1 ? ( $dr * $size + 1,          ($dc+1)*$size - $pos   )
                  : $dd == 2 ? ( ($dr+1)*$size - $pos,   ( $dc+1) * $size       )
                  :            ( ($dr+1)*$size,        $dc * $size + 1 + $pos )
                  ;
      $cube[$d]{$c}{$r} = $map[$nr][$nc] eq '#' ? undef : [$nr,$nc,$dd,$pos];
    }
  }
}
```

The final part we walk along the path - as we have our teleport "jump" locations in both cases
we can use the same code for both (in a loop) if you hit on a teleport pad it will automatically
jump you to the next space...

```perl
for my $jumps ( \@jump, \@cube ) {
  my($r,$c,$d) = (1,$mn_x[1],0);
  for( @path ) {
    $_ eq 'R' ? ($d++,$d%=4) :
    $_ eq 'L' ? ($d--,$d%=4) :
    map { exists $jumps->[$d]{$c}{$r}
    ? defined $jumps->[$d]{$c}{$r} ? ($r,$c,$d) = @{$jumps->[$d]{$c}{$r}} : next
    : $map[ $r+$dir[$d][1] ][ $c+$dir[$d][0] ] eq '.' ? ($r+=$dir[$d][1],$c+=$dir[$d][0]) : next
    } 1..$_
  }
  say $r * 1000 + $c * 4 + $d;
}
```

## Description

The monkeys take you on a surprisingly easy trail through the jungle. They're even going in roughly the right direction according to your handheld device's Grove Positioning System.

As you walk, the monkeys explain that the grove is protected by a force field. To pass through the force field, you have to enter a password; doing so involves tracing a specific path on a strangely-shaped board.

At least, you're pretty sure that's what you have to do; the elephants aren't exactly fluent in monkey.

The monkeys give you notes that they took when they last saw the password entered (your puzzle input).

For example:

```
        ...#
        .#..
        #...
        ....
...#.......#
........#...
..#....#....
..........#.
        ...#....
        .....#..
        .#......
        ......#.

10R5L5R10L4R5L5
```

The first half of the monkeys' notes is a map of the board. It is comprised of a set of open tiles (on which you can move, drawn `.``) and solid walls (tiles which you cannot enter, drawn `#`).

The second half is a description of the path you must follow. It consists of alternating numbers and letters:

A number indicates the number of tiles to move in the direction you are facing. If you run into a wall, you stop moving forward and continue with the next instruction.
A letter indicates whether to turn 90 degrees clockwise (`R`) or counterclockwise (`L`). Turning happens in-place; it does not change your current tile.
So, a path like `10R5` means "go forward 10 tiles, then turn clockwise 90 degrees, then go forward 5 tiles".

You begin the path in the leftmost open tile of the top row of tiles. Initially, you are facing to the right (from the perspective of how the map is drawn).

If a movement instruction would take you off of the map, you wrap around to the other side of the board. In other words, if your next tile is off of the board, you should instead look in the direction opposite of your current facing as far as you can until you find the opposite edge of the board, then reappear there.

For example, if you are at `A` and facing to the right, the tile in front of you is marked `B`; if you are at `C` and facing down, the tile in front of you is marked `D`:

```
        ...#
        .#..
        #...
        ....
...#.D.....#
........#...
B.#....#...A
.....C....#.
        ...#....
        .....#..
        .#......
        ......#.
```
It is possible for the next tile (after wrapping around) to be a wall; this still counts as there being a wall in front of you, and so movement stops before you actually wrap to the other side of the board.

By drawing the last facing you had with an arrow on each tile you visit, the full path taken by the above example looks like this:

```
        >>v#    
        .#v.    
        #.v.    
        ..v.    
...#...v..v#    
>>>v...>#.>>    
..#v...#....    
...>>>>v..#.    
        ...#....
        .....#..
        .#......
        ......#.
```
To finish providing the password to this strange input device, you need to determine numbers for your final row, column, and facing as your final position appears from the perspective of the original map. Rows start from 1 at the top and count downward; columns start from 1 at the left and count rightward. (In the above example, row 1, column 1 refers to the empty space with no tile on it in the top-left corner.) Facing is `0` for right (`>`), `1` for down (`v`), `2` for left (`<`), and `3` for up (`^`). The final password is the sum of `1000` times the row, `4` times the column, and the facing.

In the above example, the final row is `6`, the final column is `8`, and the final facing is `0`. So, the final password is `1000 * 6 + 4 * 8 + 0`: `6032`.

Follow the path given in the monkeys' notes. What is the final password?

Your puzzle answer was `80392`.

## Part 2

As you reach the force field, you think you hear some Elves in the distance. Perhaps they've already arrived?

You approach the strange input device, but it isn't quite what the monkeys drew in their notes. Instead, you are met with a large cube; each of its six faces is a square of 50x50 tiles.

To be fair, the monkeys' map does have six 50x50 regions on it. If you were to carefully fold the map, you should be able to shape it into a cube!

In the example above, the six (smaller, 4x4) faces of the cube are:

```
        1111
        1111
        1111
        1111
222233334444
222233334444
222233334444
222233334444
        55556666
        55556666
        55556666
        55556666
```
You still start in the same position and with the same facing as before, but the wrapping rules are different. Now, if you would walk off the board, you instead proceed around the cube. From the perspective of the map, this can look a little strange. In the above example, if you are at A and move to the right, you would arrive at B facing down; if you are at C and move down, you would arrive at D facing up:

```
        ...#
        .#..
        #...
        ....
...#.......#
........#..A
..#....#....
.D........#.
        ...#..B.
        .....#..
        .#......
        ..C...#.
```
Walls still block your path, even if they are on a different face of the cube. If you are at E facing up, your movement is blocked by the wall marked by the arrow:

```
        ...#
        .#..
     -->#...
        ....
...#..E....#
........#...
..#....#....
..........#.
        ...#....
        .....#..
        .#......
        ......#.
```
Using the same method of drawing the last facing you had with an arrow on each tile you visit, the full path taken by the above example now looks like this:

```
        >>v#    
        .#v.    
        #.v.    
        ..v.    
...#..^...v#    
.>>>>>^.#.>>    
.^#....#....    
.^........#.    
        ...#..v.
        .....#v.
        .#v<<<<.
        ..v...#.
```
The final password is still calculated from your final position and facing from the perspective of the map. In this example, the final row is `5`, the final column is `7`, and the final facing is `3`, so the final password is `1000 * 5 + 4 * 7 + 3` = `5031`.

Fold the map into a cube, then follow the path given in the monkeys' notes. What is the final password?

Your puzzle answer was `19534`.

## Folding the cube

Not sure this is very good {and it doesn't check for the net being valid...}

```perl
sub get_cube_join {
  my @cj;
  ## Sets of adjacent corners - useful for finding which neighbour is missing!
  my %N = ( 'A' => [ 'B','C','G' ], 'B' => [ 'A','D','H' ], 'C' => [ 'A','D','E' ], 'D' => [ 'B','C','F' ],
          'E' => [ 'C','F','G' ], 'F' => [ 'D','E','H' ], 'G' => [ 'E','H','A' ], 'H' => [ 'F','G','B' ],);
  ## Array of lists of verticies
  my @V = map { [ map { $_ ? ['.','.','.','.'] : [] } @{$_}] } @net;

  my $DONE=0;
  ## We start by finding 3 squares in a row...
  if($H>2) { ## Look for veritcal lines...
    O: for my $y (0..$H-3) {
      for my $x (0..$W-1) {
        next unless $net[$y][$x] && $net[$y+1][$x] && $net[$y+2][$x];
        $V[$y][$x]=['A','B','D','C'],$V[$y+1][$x]=['C','D','F','E'],$V[$y+2][$x]=['E','F','H','G'];
        $DONE=1;
        last O;
      }
    }
  }

  if($W>2 && ! $DONE) { ## or for 3 squares in a column...
    O2: for my $x (0..$W-3) {
      for my $y (0..$H-1) {
        next unless $net[$y][$x] && $net[$y][$x+1] && $net[$y][$x+2];
        $V[$y][$x  ]=['A','B','F','E'];$V[$y][$x+1]=['B','C','G','F'];$V[$y][$x+2]=['C','D','H','G'];
        $DONE=1;
        last O2;
      }
    }
  }
  
  ## So we have 3 squares laid out in a line and their vertices labelled on their faces
  
  my $changes = 1;
  while($changes>0){
    ## Stage 2 we are looking at neighbouring faces and copying vertex's labels to each connected one
    $changes = 0;
    for my $x (0..$W-1) { ## Look for faces above and below
      for my $y (1..$H-1) {
        next unless $net[$y-1][$x] && $net[$y][$x];
        $changes++,$V[$y-1][$x][3] = $V[$y  ][$x][0] if $V[$y  ][$x][0] ne '.' && $V[$y-1][$x][3] eq '.';
        $changes++,$V[$y  ][$x][0] = $V[$y-1][$x][3] if $V[$y-1][$x][3] ne '.' && $V[$y  ][$x][0] eq '.';
        $changes++,$V[$y-1][$x][2] = $V[$y  ][$x][1] if $V[$y  ][$x][1] ne '.' && $V[$y-1][$x][2] eq '.';
        $changes++,$V[$y  ][$x][1] = $V[$y-1][$x][2] if $V[$y-1][$x][2] ne '.' && $V[$y  ][$x][1] eq '.';
      }
    }
    for my $x (1..$W-1) { ## Now for faces either side of current face
      for my $y (0..$H-1) {
        next unless $net[$y][$x-1] && $net[$y][$x];
        $changes++,$V[$y][$x-1][1] = $V[$y][$x  ][0] if $V[$y][$x  ][0] ne '.' && $V[$y][$x-1][1] eq '.';
        $changes++,$V[$y][$x  ][0] = $V[$y][$x-1][1] if $V[$y][$x-1][1] ne '.' && $V[$y][$x  ][0] eq '.';
        $changes++,$V[$y][$x-1][2] = $V[$y][$x  ][3] if $V[$y][$x  ][3] ne '.' && $V[$y][$x-1][2] eq '.';
        $changes++,$V[$y][$x  ][3] = $V[$y][$x-1][2] if $V[$y][$x-1][2] ne '.' && $V[$y][$x  ][3] eq '.';
      }
    }
    ## Stage 3 the nasty one - we are looking for nodes which we have 5 of the "sides" {each side has an apex and 

    for my $x (0..$W-1) { ## Now look for ones where we know 2 of the sides but not the third.
      for my $y (0..$H-1) {
        next unless $net[$y][$x];
        for ( [0,1],[1,2],[2,3],[3,0],[1,0],[0,3],[3,2],[2,1] ) {
          if( $V[$y][$x][$_->[0]] ne '.' && $V[$y][$x][$_->[1]] eq '.' ){
            my $F = $V[$y][$x][$_->[0]];
            my %f = map { $_ => -2 } @{$N{$F}};
            for my $xx (0..$W-1) {
              for my $yy (0..$H-1) {
                next unless $net[$yy][$xx];
                for ( [0,1],[1,2],[2,3],[3,0],[1,0],[0,3],[3,2],[2,1] ) {
                  if( $V[$yy][$xx][$_->[0]] eq $F ) {
                    $f{ $V[$yy][$xx][$_->[1]] } ++;
                  }
                }
              }
            }
            delete $f{'.'};
            my $v = "@{[ sort values %f ]}";
            if( $v eq '-1 0 0' ) {
              my($T) = grep { $f{$_} } keys %f;
              $V[$y][$x][$_->[1]] = $T;
              $changes++;
            }
          }
        }
      }
    }
  }
 # show_net( \@net, \@V );

  ## This is how we join the cubest together!!!

  my $sides = {};
  for my $x (0..$W-1) {
    for my $y (0..$H-1) {
      next unless $net[$y][$x];
      for my $d (0..3) {
        push @{$sides->{"@{[sort $V[$y][$x][(1+$d)%4], $V[$y][$x][(2+$d)%4]]}"}}, [$y,$x,$d];
      }
    }
  }

  for my $x (0..$W-1) {
    for my $y (0..$H-1) {
      $cj[$y][$x]=undef, next unless $net[$y][$x];
      for my $d (0..3) {
        $cj[$y][$x][$d] = [$_->[0],$_->[1],($_->[2]-2)%4] for grep { $y!=$_->[0] && $x!=$_->[1] }
          @{$sides->{"@{[sort $V[$y][$x][(1+$d)%4], $V[$y][$x][(2+$d)%4]]}"}};
      }
    }
  }
  return @cj;
}

```
