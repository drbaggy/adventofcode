# Day 16: Proboscidea Volcanium

**[Home](README.md)** - **[Description](#description)** - **Files:** [Solution to day 16](16.pl), [Input for day 16](data/16.txt), [Output for day 16](out/16.txt).

## Solution

So to solve the problem (as fast as possible) we need to:

 1) Parse the data:
    * and get a map of the passages
    * get a list of "nodes" in our grid - either the start point OR valves with non-zero flow rate.

 2) Given the information from stage 1
    * work out the distances from one node to adjacent nodes
    * work out the shortest distance from any node to any other node.    
 
 3) Walk around the pipes turning on valves to solve part 1

 4) Munging the data from 1 to solve part 2...

## Stage 1 - load in data...

We have a simple parser - that reads the files 1-by-1. For each
row we mark down the links from valve to valve.

For either the "AA" valve or any other valve with non-zero flow
we record:

 * The flow-rate {@v}
 * The name      {@map}
 * map from name to id (%ix)
 * Distance (to itself) (@nd)

```perl
my ($i, $t, @v, @map, %ix, @nd, %mp, %ch )=(0,0);
while(<>) {
  m{Valve (\w+).*rate=(\d+).*valves? (.*)};
  $mp{ $1 }{ $_ } = $mp{ $_ }{ $1 } = 1 for split /, /,$3;
  if( $2 > 0 || $1 eq 'AA' ) {
    my $t = $ix{ $1 } ||= $1 eq 'AA' ? 0 : ++$i;
    $v[   $t ]       = $2;
    $map[ $t ]       = $1;
    $nd[  $t ][ $t ] = 0;
  }
}
```

## Stage 2 - simplify data...

We now have to simplify the data - ideally we want to know the quickest way to get between
2 valves with positive flow.

### Find all neighbours

We find all the neighbours for a node, by having a walk around the map...

```perl
discovery(     $_, $map[$_], 0, '' ) for 0..$i;  ## Find walks to nearest "node"
```

The script below recurses around the tree to find all nodes attached to a
particular node {and finds the shortest if there are two "direct" connections}

Note as well as storing the current node - we keep track of the previous node
this stops us turning back on ourselves when moving from one node to another.

```perl
sub discovery {
  my( $r, $c, $l, $p ) = @_;
  ## ($r)oot node, ($c)urrent node, ($l)ength of path, ($p)revious node
  if( $l && exists $ix{$c} ) {
    $nd[$r][$ix{$c}] = $nd[$ix{$c}][$r] = $l
      unless $nd[$ix{$c}][$r] && $nd[$ix{$c}][$r] > $l;
    return;
  }
  discovery( $r, $_, $l+1, $c ) for grep { $_ ne $p } keys %{$mp{$c}};
}
```
  
For this data set we get the following connections:
```
   AA JX IK WH CF QQ VK WL DS WM EZ LB BV HG UY IT
AA  .  .  2  .  .  .  3  .  2  .  2  .  .  .  3  .
JX  .  .  .  .  3  .  .  .  .  .  .  2  3  .  .  .
IK  2  .  .  .  .  .  2  .  3  3  .  .  .  .  3  .
WH  .  .  .  .  .  .  .  2  .  .  .  .  .  .  .  2
CF  .  3  .  .  .  .  .  .  .  .  .  3  2  .  .  .
QQ  .  .  .  .  .  .  .  .  2  .  .  .  .  .  .  .
VK  3  .  2  .  .  .  .  .  3  .  .  .  .  .  3  2
WL  .  .  .  2  .  .  .  .  .  .  .  .  .  3  .  .
DS  2  .  3  .  .  2  3  .  .  .  .  .  .  .  2  .
WM  .  .  3  .  .  .  .  .  .  .  3  3  2  .  .  .
EZ  2  .  .  .  .  .  .  .  .  3  .  .  2  .  .  .
LB  .  2  .  .  3  .  .  .  .  3  .  .  3  .  3  .
BV  .  3  .  .  2  .  .  .  .  2  2  3  .  .  .  .
HG  .  .  .  .  .  .  .  3  .  .  .  .  .  .  .  .
UY  3  .  3  .  .  .  3  .  2  .  .  3  .  .  .  .
IT  .  .  .  2  .  .  2  .  .  .  .  .  .  .  .  .
```

### Find distances to all nodes.

Secondly we "walk" again... this isn't particularly efficient.. but
compared to stage 3 it's trivial

```perl
discovery_pt2( );
```

For each node - we go to each neigbour and move one step along to another node
if we haven't made that connection before OR if we have and it was longer then
we update the distance between the two nodes. If we have made any updates we
repeat this...

```perl
sub discovery_pt2 {
  for my $x (0..$i) {
    my $u = 1;
    while( $u ) {
      $u = 0;
      for my $y (0..$i) {
        next if $x==$y || ! $nd[$x][$y];
        for (0..$i) {
          next if $_==$y || $_==$x || ! $nd[$y][$_];
          my $t = $nd[$x][$y] + $nd[$y][$_];
          ($nd[$x][$_],$u)=($t,1) unless $nd[$x][$_] && $nd[$x][$_] <= $t;
        }
      }
    }
  }
}
```

For this data set we get, this allows stage 3 to be performed more easily as
we only have one type of step - walk and open tap - we don't have to think
about walk? do I open tap or walk on...

```
   AA JX IK WH CF QQ VK WL DS WM EZ LB BV HG UY IT
AA  .  7  2  7  6  4  3  9  2  5  2  6  4 12  3  5
JX  7  .  8 12  3  9  8 14  7  5  5  2  3 17  5 10
IK  2  8  .  6  7  5  2  8  3  3  4  6  5 11  3  4
WH  7 12  6  . 13  9  4  2  7  9  9 10 11  5  7  2
CF  6  3  7 13  . 10  9 15  8  4  4  3  2 18  6 11
QQ  4  9  5  9 10  .  5 11  2  8  6  7  8 14  4  7
VK  3  8  2  4  9  5  .  6  3  5  5  6  7  9  3  2
WL  9 14  8  2 15 11  6  .  9 11 11 12 13  3  9  4
DS  2  7  3  7  8  2  3  9  .  6  4  5  6 12  2  5
WM  5  5  3  9  4  8  5 11  6  .  3  3  2 14  6  7
EZ  2  5  4  9  4  6  5 11  4  3  .  5  2 14  5  7
LB  6  2  6 10  3  7  6 12  5  3  5  .  3 15  3  8
BV  4  3  5 11  2  8  7 13  6  2  2  3  . 16  6  9
HG 12 17 11  5 18 14  9  3 12 14 14 15 16  . 12  7
UY  3  5  3  7  6  4  3  9  2  6  5  3  6 12  .  5
IT  5 10  4  2 11  7  2  4  5  7  7  8  9  7  5  .
```
## Stage 3 - solve part 1...

Our call looks nice and simple - but lets go under the hood.

```perl
my $n = walk( 30, 0, 0, 0, 0 .. $i );
```

Our walk function takes 6 parameters:

  * `$ht` - Time left
  * `$pt` - "Path"
  * `$pr` - Total pressure released (Human)
  * `$pr2` - Total pressure released (Elephant)
  * `$x`  - The current node
  * `@n`  - All unvisited nodes...

The *Elephant in the room* is why do we talk about
elephants here... Well as we are walking we can get
data we need for the 2nd part of the question

For each possible future nodes `@n` we do

 * Find the time the walk will finish
   * if this is after the end time - do nothing
   * if there is less than 5 seconds left [ we only move the human ]
     * call walk and check to see if the result is greater than the
       greatest pressure (update greatest value if it is
   * if there are more than 4 seconds lefft [ we can move human and elephant ]
     * we do the same as above BUT we also:
     * work out the additional pressure release if the valve is opened.
     * If this is greater than obtained for any other combinations of that
       set of valves open update the cache.

 * We keep track of the "path" using a binary representation with each bit
   representing a given node... this means we can use a single integer to
   key for the set of nodes.

```perl
sub walk {
  my( $ht, $pt, $pr, $pr2, $x, @n ) = @_;
  my $p = $pr;
  for(1..@n) {
    my $y = shift @n;
    my $h  = $ht - $nd[$x][$y]-1;
    if( $h > 0 ) {
      if( $h < 5 ) {
        my $v = walk( $h, $pt, $pr + $h*$v[$y], $pr2, $y, @n );
        $p = $v if $v > $p;
      } else {
        my($p2,$np2) = ( $pt | 1<<$y, $pr2 + ($h-4)*$v[$y] );
        $ch{ $p2 } = $np2 unless exists $ch{$p2} && $np2 <= $ch{ $p2 };
        my $v = walk( $h, $p2, $pr + $h*$v[$y], $np2, $y, @n );
        $p = $v if $v > $p;
      }
    }
    push @n,$y;
  }
  return $p;
}
```
## Stage 4 - solve part 2...

We do not have to do any "clever" walking to solve part 2. While solving part two we
stored the best score achievable by either a human or elephant in 26 seconds. We just
need to find the best combination of two that have no nodes in common. As we used the
node ID as the bit position - we just need to find two values in the cache for where
there are no common bits ( `$k1 & $k2 == 0` ).

We have initially a search space of about 7 million combinations. We can reduce this
futher by ruling out whole swaithes of the search space.

If we sort the results in reverse pressure order. When we loop through the arrays
we can no when to go onto the next loop. As we know through each iteration of the loop
the sum of pressures will be decreasing.

So when the sum of values is less than the current best value we know we can jump to
the next outer loop. Further more - for each value in the list - the sum of it with
any values to come will be less than twice it's value - so we can abandon the outer
loop.

```perl
my @K = sort { $ch{$b} <=> $ch{$a} || $a <=> $b } keys %ch;
while( my $k = shift @K ) {      ## Loop through the cache to find pairs of
                                 ## disjoint keys
  last if 2*(my $p=$ch{$k})<=$t; ## Jump out if the twice the value < best
                                 ## as we know we can't get a better answer
  ($p + $ch{$_} <= $t)?last:$k & $_ || ($t=$ch{$_}+$p) for @K;
     ## This values are all subsequent ones are less than best value
     ## If disjoint update best score...
  }
}
```

## Timings

My original algorithm was spectactularly slow - well over an hour - something I'm not
proud of but it got me "two stars" this is probably iteration 5 or 6. That helped
reduce time to run the script down to 0.6 to 0.7 range.

Speed ups:

 * Reduce the complexity of walk (and to optimize our some unncessary code)
 * Realise that you can use the & trick to do exclusive routes.
 * Realise that you had to walk once as you can get the elephant cache out of the first walk without really doing anything
 * Realise that your code was making the cache too large!
 * Note that sorting the cache in reverse order of pressure - made it possible to jump out of both loops when finding the solution to part 2!

### Breakdown:

These are figures from one run:

| Action      | Time for action | Cumulative time |
| :---------- | --------------: | --------------: |
| Parse       |       0.000 579 |       0.000 579 |
| Neighbours  |       0.003 838 |       0.004 417 |
| Distances   |       0.005 271 |       0.009 688 |
| Walk part 1 |       0.641 325 |       0.651 013 |
| Part 2 soln |       0.008 403 |       0.659 416 |

Annoyed I've missed the 0.5 second aim! But I'm not sure if I can wring any more cycles out of the walk method!

## Description

The sensors have led you to the origin of the distress signal: yet another handheld device, just like the one the Elves gave you. However, you don't see any Elves around; instead, the device is surrounded by elephants! They must have gotten lost in these tunnels, and one of the elephants apparently figured out how to turn on the distress signal.

The ground rumbles again, much stronger this time. What kind of cave is this, exactly? You scan the cave with your handheld device; it reports mostly igneous rock, some ash, pockets of pressurized gas, magma... this isn't just a cave, it's a volcano!

You need to get the elephants out of here, quickly. Your device estimates that you have 30 minutes before the volcano erupts, so you don't have time to go back out the way you came in.

You scan the cave for other options and discover a network of pipes and pressure-release valves. You aren't sure how such a system got into a volcano, but you don't have time to complain; your device produces a report (your puzzle input) of each valve's flow rate if it were opened (in pressure per minute) and the tunnels you could use to move between the valves.

There's even a valve in the room you and the elephants are currently standing in labeled AA. You estimate it will take you one minute to open a single valve and one minute to follow any tunnel from one valve to another. What is the most pressure you could release?

For example, suppose you had the following scan output:

```
Valve AA has flow rate=0; tunnels lead to valves DD, II, BB
Valve BB has flow rate=13; tunnels lead to valves CC, AA
Valve CC has flow rate=2; tunnels lead to valves DD, BB
Valve DD has flow rate=20; tunnels lead to valves CC, AA, EE
Valve EE has flow rate=3; tunnels lead to valves FF, DD
Valve FF has flow rate=0; tunnels lead to valves EE, GG
Valve GG has flow rate=0; tunnels lead to valves FF, HH
Valve HH has flow rate=22; tunnel leads to valve GG
Valve II has flow rate=0; tunnels lead to valves AA, JJ
Valve JJ has flow rate=21; tunnel leads to valve II
```

All of the valves begin **closed**. You start at valve `AA`, but it must be damaged or jammed or something: its flow rate is `0`, so there's no point in opening it. However, you could spend one minute moving to valve BB and another minute opening it; doing so would release pressure during the remaining `28` minutes at a flow rate of `13`, a total eventual pressure release of `28 * 13 = 364`. Then, you could spend your third minute moving to valve `CC` and your fourth minute opening it, providing an additional `26` minutes of eventual pressure release at a flow rate of `2`, or `52` total pressure released by valve `CC`.

Making your way through the tunnels like this, you could probably open many or all of the valves by the time 30 minutes have elapsed. However, you need to release as much pressure as possible, so you'll need to be methodical. Instead, consider this approach:

```
== Minute 1 ==
No valves are open.
You move to valve DD.

== Minute 2 ==
No valves are open.
You open valve DD.

== Minute 3 ==
Valve DD is open, releasing 20 pressure.
You move to valve CC.

== Minute 4 ==
Valve DD is open, releasing 20 pressure.
You move to valve BB.

== Minute 5 ==
Valve DD is open, releasing 20 pressure.
You open valve BB.

== Minute 6 ==
Valves BB and DD are open, releasing 33 pressure.
You move to valve AA.

== Minute 7 ==
Valves BB and DD are open, releasing 33 pressure.
You move to valve II.

== Minute 8 ==
Valves BB and DD are open, releasing 33 pressure.
You move to valve JJ.

== Minute 9 ==
Valves BB and DD are open, releasing 33 pressure.
You open valve JJ.

== Minute 10 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You move to valve II.

== Minute 11 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You move to valve AA.

== Minute 12 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You move to valve DD.

== Minute 13 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You move to valve EE.

== Minute 14 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You move to valve FF.

== Minute 15 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You move to valve GG.

== Minute 16 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You move to valve HH.

== Minute 17 ==
Valves BB, DD, and JJ are open, releasing 54 pressure.
You open valve HH.

== Minute 18 ==
Valves BB, DD, HH, and JJ are open, releasing 76 pressure.
You move to valve GG.

== Minute 19 ==
Valves BB, DD, HH, and JJ are open, releasing 76 pressure.
You move to valve FF.

== Minute 20 ==
Valves BB, DD, HH, and JJ are open, releasing 76 pressure.
You move to valve EE.

== Minute 21 ==
Valves BB, DD, HH, and JJ are open, releasing 76 pressure.
You open valve EE.

== Minute 22 ==
Valves BB, DD, EE, HH, and JJ are open, releasing 79 pressure.
You move to valve DD.

== Minute 23 ==
Valves BB, DD, EE, HH, and JJ are open, releasing 79 pressure.
You move to valve CC.

== Minute 24 ==
Valves BB, DD, EE, HH, and JJ are open, releasing 79 pressure.
You open valve CC.

== Minute 25 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.

== Minute 26 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.

== Minute 27 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.

== Minute 28 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.

== Minute 29 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.

== Minute 30 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.
```

This approach lets you release the most pressure possible in 30 minutes with this valve layout, `1651`.

Work out the steps to release the most pressure in 30 minutes. What is the most pressure you can release?

Your puzzle answer was `1940`.

## Part 2

You're worried that even with an optimal approach, the pressure released won't be enough. What if you got one of the elephants to help you?

It would take you 4 minutes to teach an elephant how to open the right valves in the right order, leaving you with only 26 minutes to actually execute your plan. Would having two of you working together be better, even if it means having less time? (Assume that you teach the elephant before opening any valves yourself, giving you both the same full 26 minutes.)

In the example above, you could teach the elephant to help you as follows:

```
== Minute 1 ==
No valves are open.
You move to valve II.
The elephant moves to valve DD.

== Minute 2 ==
No valves are open.
You move to valve JJ.
The elephant opens valve DD.

== Minute 3 ==
Valve DD is open, releasing 20 pressure.
You open valve JJ.
The elephant moves to valve EE.

== Minute 4 ==
Valves DD and JJ are open, releasing 41 pressure.
You move to valve II.
The elephant moves to valve FF.

== Minute 5 ==
Valves DD and JJ are open, releasing 41 pressure.
You move to valve AA.
The elephant moves to valve GG.

== Minute 6 ==
Valves DD and JJ are open, releasing 41 pressure.
You move to valve BB.
The elephant moves to valve HH.

== Minute 7 ==
Valves DD and JJ are open, releasing 41 pressure.
You open valve BB.
The elephant opens valve HH.

== Minute 8 ==
Valves BB, DD, HH, and JJ are open, releasing 76 pressure.
You move to valve CC.
The elephant moves to valve GG.

== Minute 9 ==
Valves BB, DD, HH, and JJ are open, releasing 76 pressure.
You open valve CC.
The elephant moves to valve FF.

== Minute 10 ==
Valves BB, CC, DD, HH, and JJ are open, releasing 78 pressure.
The elephant moves to valve EE.

== Minute 11 ==
Valves BB, CC, DD, HH, and JJ are open, releasing 78 pressure.
The elephant opens valve EE.

(At this point, all valves are open.)

== Minute 12 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.

...

== Minute 20 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.

...

== Minute 26 ==
Valves BB, CC, DD, EE, HH, and JJ are open, releasing 81 pressure.
```

With the elephant helping, after 26 minutes, the best you could do would release a total of `1707` pressure.

With you and an elephant working together for 26 minutes, what is the most pressure you could release?

Your puzzle answer was `2469`.
