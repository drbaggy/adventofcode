# Day 18: Boiling Boulders

**[Home](README.md)** - **[Description](#description)** - **Files:** [Solution to day 18](18.pl), [Input for day 18](data/18.txt), [Output for day 18](out/18.txt).

## Solution

Thankfully today's description is short and sweet, and so thankfully was a root to the solution.

Firsty we parse the input file from the scanner into two data structures, (1) a 3d-grid of the
points and (2) just a list of co-ordinates of these points.

Additionally we compute the max x, y & z co-ordinates (after noting that the data has all +ve
values and each x, y, z have 0 values.

```perl
my($t,$n,$mx,$my,$mz,@grid)=(0,0,0,0,0);
$grid[$_->[0]+1][$_->[1]+1][$_->[2]+1]=2,
$_->[0] > $mx && ($mx = $_->[0]),
$_->[1] > $my && ($my = $_->[1]),
$_->[2] > $mz && ($mz = $_->[2]) for my @rows = map { chomp;[map{int$_}split/,/] } <>;
$mx+=2,$my+=2,$mz+=2,$n=6*@rows;
```

**Note:"" we add 1 to the co-ordinates of the grid so that when we move left/down/back from any
rock we don't have -ve co-ordinates.

## Part 1

We loop through all the co-ordinates we stored in `@rows`, and see if there is another bit of
lava beside it in any of the six directions {note we could have done the inner part with a for
loop - but even though it makes the code shorter it makes it slower!} If there is another rock
beside it - we reduce the face count by 2 {each face will be removed twice once from each
direction}.

```perl
for ( @rows ) {
  $n-- if $grid[$_->[0]  ][$_->[1]+1][$_->[2]+1];
  $n-- if $grid[$_->[0]+1][$_->[1]  ][$_->[2]+1];
  $n-- if $grid[$_->[0]+1][$_->[1]+1][$_->[2]  ];
  $n-- if $grid[$_->[0]+2][$_->[1]+1][$_->[2]+1];
  $n-- if $grid[$_->[0]+1][$_->[1]+2][$_->[2]+1];
  $n-- if $grid[$_->[0]+1][$_->[1]+1][$_->[2]+2];
}
```

## Part 2

This was a harder challenge coding wise - but actually not logically hard - we just need to flood the area
around the lava and look for the times we hit the face of the lava.

This is why we used "`2`" as the value for the lava when setting up! We use a standard flood-fill queue
model.
 * Our queue starts with one of the corners of the space - in this case (`[0,0,0`])
 * We check to see if the grid has anything in...
   * If it contains lava (`2`) we must have crossed a face of the lava so we add `1` to the count
   * If it is just water (`1`) we do more with this cell
   * If it empty (`0`) we mark it as having water (`1`), and we add the locations in all 6
     directions to the queue {if they stay within the maximum and minimum bounds}
 
```perl
my @q = ([0,0,0]);
while( $_ = shift @q ) {
  my($x,$y,$z) = @{$_};
  $t++ if (my $v = $grid[$x][$y][$z]||0) == 2;
  next if $v;
  $grid[$x][$y][$z]=1;
  push @q,$z     ? [$x,$y,$z-1] : (), $z<$mz ? [$x,$y,$z+1] : (),
          $y     ? [$x,$y-1,$z] : (), $y<$my ? [$x,$y+1,$z] : (),
          $x     ? [$x-1,$y,$z] : (), $x<$mx ? [$x+1,$y,$z] : ();
}

say "$n\n$t";
```

## Description

You and the elephants finally reach fresh air. You've emerged near the base of a large volcano that seems to be actively erupting! Fortunately, the lava seems to be flowing away from you and toward the ocean.

Bits of lava are still being ejected toward you, so you're sheltering in the cavern exit a little longer. Outside the cave, you can see the lava landing in a pond and hear it loudly hissing as it solidifies.

Depending on the specific compounds in the lava and speed at which it cools, it might be forming obsidian! The cooling rate should be based on the surface area of the lava droplets, so you take a quick scan of a droplet as it flies past you (your puzzle input).

Because of how quickly the lava is moving, the scan isn't very good; its resolution is quite low and, as a result, it approximates the shape of the lava droplet with 1x1x1 cubes on a 3D grid, each given as its `x`,`y`,`z` position.

To approximate the surface area, count the number of sides of each cube that are not immediately connected to another cube. So, if your scan were only two adjacent cubes like `1`,`1`,`1` and `2`,`1`,`1`, each cube would have a single side covered and five sides exposed, a total surface area of `10` sides.

Here's a larger example:

```
2,2,2
1,2,2
3,2,2
2,1,2
2,3,2
2,2,1
2,2,3
2,2,4
2,2,6
1,2,5
3,2,5
2,1,5
2,3,5
```

In the above example, after counting up all the sides that aren't connected to another cube, the total surface area is `64`.

What is the surface area of your scanned lava droplet?

Your puzzle answer was `4370`.

## Part 2
Something seems off about your calculation. The cooling rate depends on exterior surface area, but your calculation also included the surface area of air pockets trapped in the lava droplet.

Instead, consider only cube sides that could be reached by the water and steam as the lava droplet tumbles into the pond. The steam will expand to reach as much as possible, completely displacing any air on the outside of the lava droplet but never expanding diagonally.

In the larger example above, exactly one cube of air is trapped within the lava droplet (at `2,2,5`), so the exterior surface area of the lava droplet is `58`.

What is the exterior surface area of your scanned lava droplet?

Your puzzle answer was `2458`.
