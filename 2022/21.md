# Day 21: Monkey Math

**[Home](README.md)** - **[Description](#description)** - **Files:** [Solution to day 21](21.pl), [Input for day 21](data/21.txt), [Output for day 21](out/21.txt).

## Solution

Perhaps `root` gives us a clue that a tree solution is the best one. We parse the file into a hash of scalars/"triples". The key being the monkey name, the values being a scalar for monkey's who know there number or an arrayref with the two monkeys and an operator...

This hash is just a list of nodes - and we can easilyl traverse it.

Our walk function says:
  * Is the left node a number if so we are going to use that otherwise we are going to evaluate the sub tree
  * Is the right node a number if so we use that or otherwise evaluate the tree.
  * Now rather than a complicated `if`/`then`/`else` block we will use a dispatch table to apply the operators.
  * Our dispatch table has three methods for each operator - but here we will use the first which is how to combine two child nodes.

This nicely simplifies the tree to get the answer needed for part 1.

Now part 2 is slightly harder - as we can't fully collapse the tree... But we can partially do it. If we replace `humn` with an uncollapsable node, when we collapse the tree we end up with a single zig-zagging branch from the root node the `humn` node.

We additionally have to do something with the `root` node we don't actually need the comparator operator. We can replace the `root` operation with `-` and set a target the target to `0`.

So to get the answer we start at the root node - we will have a child node which is a number and a child node which is subtree, and a target to achieve. At each staget we can work out what the missing number is to make this target... and repeat until we get to `humn`.

Again we use a dispatch function - in this case entries 2 and 3 in the table. 2 if the sub-tree is the left-hand element, 3 if the sub-tree is the right-hand element.

```perl
my($t,@res)=(0,0);
my %fn = (
  '+' => [ sub { $_[0]+$_[1] }, sub { $_[0] - $_[1] }, sub { $_[0] - $_[1] } ],
  '-' => [ sub { $_[0]-$_[1] }, sub { $_[0] + $_[1] }, sub { $_[1] - $_[0] } ],
  '/' => [ sub { $_[0]/$_[1] }, sub { $_[0] * $_[1] }, sub { $_[1] / $_[0] } ],
  '*' => [ sub { $_[0]*$_[1] }, sub { $_[0] / $_[1] }, sub { $_[0] / $_[1] } ],
  'X' => [ sub { [] } ],
);
open my $fh,'<',$fn;
my %p = map { chomp, my @t = split /(?:: | )/; $t[0],  @t>2 ? [@t[1..3]] : $t[1] } <$fh>;
close $fh;

say walk( 'root' );

( $p{'humn'}, $p{'root'}[1] ) = ( [0,'X',0], '-'  );
my $z = walk('root');

( $t, $z )=( $fn{ $z->[1] }[ ref $z->[0] ? 1 : 2 ]( $t, $z->[ ref $z->[0] ? 2 : 0 ] ),
             $z->[ ref $z->[0] ? 0 : 2 ]        ) while $z && @$z;
say $t;

sub walk {
  my( $l, $x, $r ) = @{$p{$_[0]}};
  ( $l, $r ) = map { ref $p{$_} ? walk($_) : $p{$_} } $l, $r;
  ( ref $l || ref $r ) ? [ $l, $x, $r ] : $fn{$x}[0]( $l, $r )
}
```

and we are nicely on track with our timings - with a total run time of approximately 10ms, after day's 19 and 20 timings...
## Description

The monkeys are back! You're worried they're going to try to steal your stuff again, but it seems like they're just holding their ground and making various monkey noises at you.

Eventually, one of the elephants realizes you don't speak monkey and comes over to interpret. As it turns out, they overheard you talking about trying to find the grove; they can show you a shortcut if you answer their riddle.

Each monkey is given a job: either to yell a specific number or to yell the result of a math operation. All of the number-yelling monkeys know their number from the start; however, the math operation monkeys need to wait for two other monkeys to yell a number, and those two other monkeys might also be waiting on other monkeys.

Your job is to work out the number the monkey named root will yell before the monkeys figure it out themselves.

For example:

```
root: pppw + sjmn
dbpl: 5
cczh: sllz + lgvd
zczc: 2
ptdq: humn - dvpt
dvpt: 3
lfqf: 4
humn: 5
ljgn: 2
sjmn: drzm * dbpl
sllz: 4
pppw: cczh / lfqf
lgvd: ljgn * ptdq
drzm: hmdt - zczc
hmdt: 32
```

Each line contains the name of a monkey, a colon, and then the job of that monkey:

A lone number means the monkey's job is simply to yell that number.
A job like `aaaa + bbbb` means the monkey waits for monkeys `aaaa` and `bbbb` to yell each of their numbers; the monkey then yells the sum of those two numbers.
`aaaa - bbbb` means the monkey yells `aaaa`'s number minus `bbbb`'s number.
Job `aaaa * bbbb` will yell `aaaa`'s number multiplied by `bbbb`'s number.
Job `aaaa / bbbb` will yell `aaaa`'s number divided by `bbbb`'s number.
So, in the above example, monkey `drzm` has to wait for monkeys `hmdt` and `zczc` to yell their numbers. Fortunately, both `hmdt` and `zczc` have jobs that involve simply yelling a single number, so they do this immediately: `32` and `2`. Monkey `drzm` can then yell its number by finding `32` minus `2`: `30`.

Then, monkey `sjmn` has one of its numbers (`30`, from monkey `drzm`), and already has its other number, `5`, from `dbpl`. This allows it to yell its own number by finding `30` multiplied by `5`: `150`.

This process continues until `root` yells a number: `152`.

However, your actual situation involves considerably more monkeys. What number will the monkey named root yell?

Your puzzle answer was `168502451381566`.

## Part 2
Due to some kind of monkey-elephant-human mistranslation, you seem to have misunderstood a few key details about the riddle.

First, you got the wrong job for the monkey named root; specifically, you got the wrong math operation. The correct operation for monkey root should be `=`, which means that it still listens for two numbers (from the same two monkeys as before), but now checks that the two numbers match.

Second, you got the wrong monkey for the job starting with `humn`:. It isn't a monkey - it's you. Actually, you got the job wrong, too: you need to figure out what number you need to yell so that root's equality check passes. (The number that appears after `humn`: in your input is now irrelevant.)

In the above example, the number you need to yell to pass root's equality test is `301`. (This causes root to get the same number, `150`, from both of its monkeys.)

What number do you yell to pass root's equality test?

Your puzzle answer was `3343167719435`.
